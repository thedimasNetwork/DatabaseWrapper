import groovy.json.JsonSlurper
import org.jooq.codegen.DefaultGeneratorStrategy
import org.jooq.codegen.GenerationTool
import org.jooq.meta.Definition
import org.jooq.meta.DefaultDataTypeDefinition
import org.jooq.meta.jaxb.*
import org.jooq.tools.StringUtils

apply plugin: "java"
apply plugin: "maven-publish"

group = 'stellar.database'
version = '1.1'

sourceCompatibility = 16

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

ext {
    mindustryVersion = "v145"
    lombokVersion = "1.18.24"
    discordWebhookVersion = "v1.6"
    jacksonYamlVersion = "2.13.0"
    jdaVersion = "5.0.0-alpha.18"
    mysqlVersion = "8.0.30"
    jooqVersion = "3.17.0"
}

repositories {
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jooq:jooq:3.17.0" // older version due to older MySQL version on the host
        classpath "org.jooq:jooq-codegen:3.17.0"
        classpath "org.jooq:jooq-meta:3.17.0"
        classpath "mysql:mysql-connector-java:8.0.30"
    }
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"

    compileOnly("com.github.Anuken.MindustryJitpack:core:$mindustryVersion") { exclude module: "flabel" }
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"

    implementation "org.jooq:jooq:$jooqVersion"
    runtimeOnly "mysql:mysql-connector-java:$mysqlVersion"
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

task codegen {
    doLast {
        var host, schema, user, password
        if (!project.hasProperty("dbhost") || !project.hasProperty("dbschema") || !project.hasProperty("dbuser") || !project.hasProperty("dbpassword")) {
            if (file("./database.json").exists()) {
                println("Using local JSON config")
                var json = new JsonSlurper()
                var dbFile = file("./database.json")
                var db = json.parseText(dbFile.text)

                host = db.host
                schema = db.schema
                user = db.username
                password = db.password
            } else {
                println("\u001B[33;1mWARNING!\u001B[0m No DB config. Compiling may fail due to lack of `gen` packet")
                return
            }
        } else {
            host = project.property("dbhost")
            schema = project.property("dbschema")
            user = project.property("dbuser")
            password = project.property("dbpassword")
        }
        println(sourceSets.main.java.srcDirs.first().path)
        Configuration configuration = new Configuration()
                .withJdbc(new Jdbc()
                        .withDriver("com.mysql.cj.jdbc.Driver")
                        .withUrl("jdbc:mysql://$host:3306/$schema")
                        .withUser("$user")
                        .withPassword("$password")
                )
                .withGenerator(new Generator()
                        .withDatabase(new Database()
                                .withName("org.jooq.meta.mysql.MySQLDatabase")
                                .withIncludes(".*")
                                .withExcludes("")
                                .withInputSchema("$schema")
                                .withForcedTypes(
                                        new ForcedType()
                                                .withUserType("stellar.database.enums.MessageType")
                                                .withEnumConverter(true)
                                                .withIncludeTypes("mindustry.messages.type"),
                                        new ForcedType()
                                                .withUserType("stellar.database.enums.PlayerStatus")
                                                .withEnumConverter(true)
                                                .withIncludeTypes("mindustry.users.status"),
                                        new ForcedType()
                                                .withName("BOOLEAN")
                                                .withIncludeTypes("(?i:TINYINT)")
                                )
                        )
                        .withTarget(new Target()
                                .withPackageName("stellar.database.gen")
                                .withDirectory(sourceSets.main.java.srcDirs.first().path)
                        )
                        .withStrategy(new Strategy()
                                .withName(CamelCaseGeneratorStrategy.name)
                        )
                        .withGenerate(new Generate()
                                .withFluentSetters(true)
                        )
                )

        GenerationTool.generate(configuration)
    }
}

jar {
    println("Make sure you're using latest database version. Do `./gradle codegen` to generate code")
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    archiveFileName = "${project.archivesBaseName}.jar"
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

class CamelCaseGeneratorStrategy extends DefaultGeneratorStrategy {
    String getJavaIdentifier(Definition definition) {
        StringUtils.toCamelCaseLC(super.getJavaIdentifier(definition))
    }

    String getJavaGetterName(Definition definition, Mode mode) {
        if (mode == Mode.RECORD) {
            if (((DefaultDataTypeDefinition) definition.getProperties().get("definedType")).type.equalsIgnoreCase("tinyint")) {
                return "is" + StringUtils.toCamelCase(definition.getOutputName())
            }
        }
        return "get" + StringUtils.toCamelCase(definition.getOutputName())
    }
}

